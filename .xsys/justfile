FEED_SERVICE:="xsys-feeds.service"

help:
  just --list --unsorted

build:
  cargo build --release
alias b := build

dev:
  cargo watch -cx lbuild
alias d := dev

run CMD:
  cargo run -- {{CMD}}
alias r := run

feeds-install:
  mkdir -p ~/.config/systemd/user
  cp res/{{FEED_SERVICE}} ~/.config/systemd/user/{{FEED_SERVICE}}
  systemctl --user daemon-reload
  systemctl --user enable {{FEED_SERVICE}}

_feed-sysd CMD:
  systemctl --user {{CMD}} {{FEED_SERVICE}}

feeds-start: (_feed-sysd "start")
feeds-stop: (_feed-sysd "stop")
feeds-status: (_feed-sysd "status")
